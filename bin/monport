#!/usr/bin/perl
use strict;
use warnings;
use YAML::Tiny;
use File::Spec;
use App::Monport;
use Getopt::Long;
use Pod::Usage;

# Command line options
GetOptions(
    "h|?|help" => \( my $help ),
) or pod2usage(1);

# Help
pod2usage( -exitval => 0, -verbose => 2, -noperldoc => 1 ) if $help;

my $conf_file = File::Spec->catfile( $ENV{HOME}, '.monport.yml' );

if (@ARGV) {
    my $yaml;
    die "'$conf_file' already exists: $!\n" if -e $conf_file;
    $yaml = YAML::Tiny->new();
    for my $host (@ARGV) {
        my $open_ports = scan_ports($host);
        push @$yaml, { $host => $open_ports };
        $yaml->write($conf_file);
    }
}

my $yaml = YAML::Tiny->read($conf_file);
for my $hashref (@$yaml) {
    for my $host ( sort keys %$hashref ) {

        my $open = scan_ports($host);
        my $expected_open = $hashref->{$host} // [];

        print "$host\n";

        for my $port ( sort @$open ) {
            print "  $port is open\n"
              unless grep $port == $_, @$expected_open;
        }

        for my $port ( sort @$expected_open ) {
            print "  $port is closed\n"
              unless grep $port == $_, @$open;
        }
    }
}

__END__

=head1 NAME

monport - keep an eye on network ports

=head1 SYNOPSIS

monport [options] [hosts]

  options:
    -h, -?, --help  brief help message

=head1 DESCRIPTION

Compare the open ports against an expected state defined in a configuration
file. To create the configuration file run:

    $ monport host1 host2

To check the state of the ports:

    $ monport 

=cut
