#!/usr/bin/perl
use strict;
use warnings;
use YAML::Tiny;
use File::Spec qw(catfile);
use App::Monport qw(scan_ports);

my $conf_file = File::Spec->catfile( $ENV{HOME}, "monport.yml" );

if (@ARGV) {
    my $yaml;
    die "'$conf_file' already exists: $!" if -e $conf_file;
    $yaml = YAML::Tiny->new();
    for my $host (@ARGV) {
        my $open_ports = scan_ports($host);
        push @$yaml, { $host => $open_ports };
        $yaml->write($conf_file);
    }
}

my $yaml = YAML::Tiny->read($conf_file);

for my $hashref (@$yaml) {
    for my $host ( sort keys %$hashref ) {

        my $open = scan_ports($host);
        my $expected_open = $hashref->{$host} // [];

        print "$host\n";

        for my $port ( sort @$open ) {
            print "  $port is open\n"
              unless grep $port == $_, @$expected_open;
        }

        for my $port ( sort @$expected_open ) {
            print "  $port is closed\n"
              unless grep $port == $_, @$open;
        }
    }
}
